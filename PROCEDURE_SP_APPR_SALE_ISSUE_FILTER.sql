create PROC SP_APPR_SALE_ISSUE_FILTER
(
	 @NMODE NUMERIC(1) /*@NMODE: 3 FOR CUSTOMER LOV AND 1 FOR APPROVAL DATA*/
	,@NPARTY_TYPE NUMERIC(1)=0 /*@NPARTY_TYPE:0 FOR ALL,1 FOR LEDGER AND 2 FOR CUSTOMER*/ 
	,@NAPPROVAL_STATUS NUMERIC(1)=0 /*@NAPPROVAL_STATUS:0 FOR ALL, 1 FOR APPROVED AND 2 FOR PARTIALLY AND 3 FOR PENDING*/
	,@DFROM_DT DATETIME=''
	,@DTO_DT DATETIME=''
	,@CCUSTOMER_CODE VARCHAR(12)='' /*@CCUSTOMER_CODE: '' FOR NO FILTER, ELSE APPLY FILTER ON CUSTOMERS*/
	,@CAC_CODE VARCHAR(10)='' /*@CAC_CODE: '' FOR NO FILTER, ELSE APPLY FILTER ON LEDGERS*/
	,@CEMP_CODE VARCHAR(7)='' /*@CEMP_CODE: '' FOR NO FILTER, ELSE APPLY FILTER ON EMPLOYEES*/
	,@NCANCELLED NUMERIC(1)=0 /*@NCANCELLED: 2 FOR ALL, 1 FOR CANCELLED AND 0 FOR UNCANCELLED*/
	,@NXN_ITEM_TYPE NUMERIC(1)=0
)
AS
BEGIN
DECLARE @QUERY NVARCHAR(MAX)
DECLARE @CTABLE NVARCHAR(100)


IF @NMODE=3
BEGIN
	SELECT TOP 50
	USER_CUSTOMER_CODE,CUSTOMER_CODE,CUSTOMER_FNAME + ' ' + CUSTOMER_LNAME AS CUSTOMER_NAME
	FROM CUSTDYM 
	WHERE INACTIVE=0 AND CUSTOMER_CODE<>'000000000000' AND
	(USER_CUSTOMER_CODE LIKE '%'+@CCUSTOMER_CODE+'%' OR CUSTOMER_FNAME LIKE '%'+@CCUSTOMER_CODE+'%' OR CUSTOMER_LNAME LIKE '%'+@CCUSTOMER_CODE+'%')
END
ELSE
BEGIN
	---GETTING SINGLE EMPLOYEE FOR A MEMO
	IF OBJECT_ID('TEMPDB..#APM_EMPS','U') IS NOT NULL
		DROP TABLE #APM_EMPS
	
	SELECT DISTINCT APM.MEMO_ID
		  ,(CASE WHEN EMP_CODE<>'0000000' THEN EMP_CODE
				 WHEN EMP_CODE1<>'0000000' THEN  EMP_CODE1
				 ELSE EMP_CODE2 END) AS EMP_CODE
	INTO #APM_EMPS				 
	FROM APD01106 APD(NOLOCK)
	JOIN APM01106 APM(NOLOCK) ON APD.MEMO_ID=APM.MEMO_ID
	WHERE APM.MEMO_DT BETWEEN @DFROM_DT AND @DTO_DT
	AND (ISNULL(@CEMP_CODE,'')='' OR APD.EMP_CODE=@CEMP_CODE OR APD.EMP_CODE1=@CEMP_CODE OR APD.EMP_CODE2=@CEMP_CODE)
	and (@NXN_ITEM_TYPE =0 or case when isnull(APM.XN_ITEM_TYPE,0) in(0,1)  then 1 else APM.XN_ITEM_TYPE end =@NXN_ITEM_TYPE )
	
	;WITH DUPEMP 
	AS
	(
		SELECT MEMO_ID,EMP_CODE,ROW_NUMBER() OVER(PARTITION BY MEMO_ID ORDER BY EMP_CODE DESC) AS DUP_CNT 
		FROM #APM_EMPS
	)
	DELETE DUPEMP WHERE DUP_CNT>1
	
	SELECT 
	A.MEMO_NO,A.MEMO_DT,A.MEMO_ID,  
	(CASE WHEN A.CANCELLED = 1 THEN 0  ELSE ISNULL(D.TOTAL_QUANTITY,0) END) AS TOTAL_QUANTITY,    
	(CASE WHEN A.CANCELLED = 1 THEN 0  ELSE A.SUBTOTAL END) AS SUBTOTAL,    
	(CASE WHEN A.CANCELLED = 1 THEN 0  ELSE A.NET_AMOUNT END) AS NET_AMOUNT,    							
	B.USER_CUSTOMER_CODE,
	B.CUSTOMER_FNAME + ' ' + B.CUSTOMER_LNAME  AS CUSTOMER_NAME,    
	C.AC_NAME,
	(CASE WHEN A.CANCELLED = 1 THEN 0  ELSE A.ROUND_OFF END) AS ROUND_OFF,  
	A.DISCOUNT_PERCENTAGE,A.ATD_CHARGES AS OTHER_CHARGES,   
	(CASE WHEN A.CANCELLED = 1 THEN 0  ELSE A.DISCOUNT_AMOUNT END) AS DISCOUNT_AMOUNT ,    
	(CASE WHEN A.CANCELLED = 1 THEN 'CANCELLED' ELSE '' END) AS CANCELLED,  
	(CASE WHEN A.MEMO_TYPE = 1 THEN 'SALE APPROVAL' ELSE 'WHOLE SALE APPROVAL' END) AS MEMO_TYPE,  
	(CASE WHEN ISNULL(E.COUNT_ROWP,0)<= 0 THEN 'APPROVAL SETTELED' ELSE   
	(CASE WHEN ISNULL(E.COUNT_ROWP,0)>0 AND ISNULL(F.COUNT_ROW,0)> 0 THEN 'PARTIALLY SETTELED' ELSE 'PENDING' END )END) AS STATUS,  
	A.FIN_YEAR ,EMP.EMP_NAME AS SALE_PERSON
	FROM APM01106 A (NOLOCK)     
	LEFT JOIN CUSTDYM B (NOLOCK) ON A.CUSTOMER_CODE = B.CUSTOMER_CODE    
	LEFT JOIN LMV01106 C (NOLOCK) ON A.AC_CODE = C.AC_CODE    
	LEFT OUTER JOIN     
	(
		SELECT APM.MEMO_ID,SUM(QUANTITY) AS TOTAL_QUANTITY
		FROM APD01106 APD(NOLOCK)
		JOIN APM01106 APM(NOLOCK) ON APD.MEMO_ID=APM.MEMO_ID
		WHERE APM.MEMO_DT BETWEEN @DFROM_DT AND @DTO_DT
		GROUP BY  APM.MEMO_ID
	)D ON D.MEMO_ID=A.MEMO_ID   
	JOIN #APM_EMPS AE ON AE.MEMO_ID=A.MEMO_ID
	JOIN EMPLOYEE EMP ON EMP.EMP_CODE = AE.EMP_CODE
 --   LEFT OUTER JOIN 
	--(
	-- SELECT EMP_CODE,EMP_NAME FROM EMPLOYEE
	--) EMP ON EMP.EMP_CODE = AE.EMP_CODE
	LEFT OUTER JOIN  
	(  
		SELECT COUNT(A.MEMO_ID) AS COUNT_ROWP, A.MEMO_ID,A.MEMO_TYPE  
		FROM APM01106 A   
		JOIN APD01106 B ON A.MEMO_ID = B.MEMO_ID           
		LEFT OUTER JOIN           
		(
			SELECT APD_ROW_ID,C.MEMO_TYPE,SUM(A.QUANTITY) AS RET_QTY 
			FROM APPROVAL_RETURN_DET A         
			JOIN APPROVAL_RETURN_MST MST ON A.MEMO_ID=MST.MEMO_ID         
			JOIN APD01106 B ON A.APD_ROW_ID=B.ROW_ID       
			JOIN APM01106 C ON C.MEMO_ID=B.MEMO_ID          
			WHERE C.CANCELLED=0 AND MST.CANCELLED=0      
			GROUP BY APD_ROW_ID,C.MEMO_TYPE
		) C ON C.APD_ROW_ID=B.ROW_ID  AND C.MEMO_TYPE = A.MEMO_TYPE  
		WHERE (B.QUANTITY-ISNULL(C.RET_QTY,0)) > 0    
		GROUP BY A.MEMO_ID,A.MEMO_TYPE   
	)E ON A.MEMO_ID = E.MEMO_ID AND A.MEMO_TYPE = E.MEMO_TYPE  
	LEFT OUTER JOIN  
	(  							  
		SELECT COUNT(APM.MEMO_ID) AS COUNT_ROW,APM.MEMO_ID,APM.MEMO_TYPE   
		FROM APPROVAL_RETURN_DET A  
		JOIN APD01106 APD ON A.APD_ROW_ID=APD.ROW_ID  
		JOIN APM01106 APM ON APM.MEMO_ID=APD.MEMO_ID  
		GROUP BY APM.MEMO_ID,APM.MEMO_TYPE  
	) F ON A.MEMO_ID = F.MEMO_ID AND A.MEMO_TYPE = F.MEMO_TYPE  							
	WHERE A.MEMO_DT BETWEEN @DFROM_DT AND @DTO_DT
	
	AND (ISNULL(@NPARTY_TYPE,0)=0 OR (@NPARTY_TYPE=1 AND ISNULL(A.AC_CODE,'0000000000')<>'0000000000')
			OR (@NPARTY_TYPE=2 AND ISNULL(A.CUSTOMER_CODE,'000000000000')<>'000000000000'))
	AND (@NAPPROVAL_STATUS=0
	    OR (@NAPPROVAL_STATUS=1 AND ISNULL(E.COUNT_ROWP,0)<= 0)
		OR (@NAPPROVAL_STATUS=2 AND ISNULL(E.COUNT_ROWP,0)>0 AND ISNULL(F.COUNT_ROW,0)> 0)
		OR (@NAPPROVAL_STATUS=3 AND ISNULL(E.COUNT_ROWP,0)>0 AND ISNULL(F.COUNT_ROW,0)<= 0)
		)
	AND (ISNULL(@CCUSTOMER_CODE,'')='' OR A.CUSTOMER_CODE=@CCUSTOMER_CODE)
	AND (ISNULL(@CAC_CODE,'')='' OR A.AC_CODE=@CAC_CODE)
	AND (ISNULL(@NCANCELLED,2)=2 OR A.CANCELLED=@NCANCELLED)
	ORDER BY A.MEMO_ID
END
END
