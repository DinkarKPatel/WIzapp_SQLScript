CREATE PROCEDURE SP3S_UPDSKU_IMG
--WITH ENCRYPTION
AS
BEGIN
BEGIN TRY
	DECLARE @CIMAGEPATH SYSNAME,@CERRMSG VARCHAR(MAX)

	SELECT TOP 1 @CIMAGEPATH=VALUE FROM CONFIG WHERE CONFIG_OPTION='ART_PICT_PATH'

	IF ISNULL(@CIMAGEPATH,'')=''
	BEGIN
		SET @CERRMSG='NO IMAGE PATH DEFINED IN CONFIG'
		GOTO END_PROC
	END

	IF OBJECT_ID('TEMPDB..#FILEPATH','U') IS NOT NULL
		DROP TABLE #FILEPATH
		
	CREATE TABLE #FILEPATH(ID INT IDENTITY(1,1),SUB_DIRECTORY NVARCHAR(MAX),DEPTH NUMERIC(2),ISFILE BIT
						  ,PARENT_DIRECTORYID INT,DT_CREATED NVARCHAR(MAX))	
					
	INSERT #FILEPATH(SUB_DIRECTORY,DEPTH,ISFILE)				
	EXEC MASTER..XP_DIRTREE @CIMAGEPATH,2,1				

	IF NOT EXISTS(SELECT TOP 1 'U' FROM #FILEPATH WHERE ISFILE=1)
	BEGIN
		SET @CERRMSG='NO IMAGE FOUND IN THE SPECIFIED PATH'
		GOTO END_PROC
	END

	--GETTING THE PARENT DIRECTORY ID
	UPDATE A SET PARENT_DIRECTORYID=
	(
		SELECT MAX(ID) 
		FROM #FILEPATH B
		WHERE B.ISFILE=0 AND A.ISFILE=1 AND B.ID<A.ID
	)
	FROM #FILEPATH A

	--SELECT * FROM #FILEPATH
	UPDATE A SET DT_CREATED=B.SUB_DIRECTORY
	FROM #FILEPATH A
	JOIN #FILEPATH B ON A.PARENT_DIRECTORYID=B.ID

	UPDATE A SET DT_CREATED=B.DT_CREATED
	FROM SKU A
	JOIN #FILEPATH B ON A.PRODUCT_CODE+'.JPG'=B.SUB_DIRECTORY
	WHERE A.DT_CREATED='19000101' AND B.ISFILE=1

END TRY
BEGIN CATCH
	SET @CERRMSG=N'ERROR : '+ERROR_MESSAGE()
END CATCH

END_PROC:
	SELECT ISNULL(@CERRMSG,'') AS ERRMSG
END
