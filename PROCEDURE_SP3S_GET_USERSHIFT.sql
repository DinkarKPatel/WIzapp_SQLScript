CREATE PROCEDURE SP3S_GET_USERSHIFT
(
	@CUSER_CODE VARCHAR(7),
	@CLOCID		VARCHAR(5)=''
	
)
--WITH ENCRYPTION 
AS 
BEGIN
--EXEC SP3S_GET_USERSHIFT 'RA00013'
	DECLARE @CSHIFT_ID VARCHAR(15),@CERRMSG VARCHAR(500),@CUSER_NAME VARCHAR(30)
		   ,@NCOMP_OP NUMERIC(18,2),@NRECEIPTS NUMERIC(18,2),@NISSUES NUMERIC(18,2),
		   @CDEPT_ID VARCHAR(10),@NSHIFTAMT NUMERIC(18,2),@CCASHIERENABLED VARCHAR(1)
	
	
	
	SELECT @CUSER_NAME=USERNAME FROM USERS WHERE USER_CODE=@CUSER_CODE
	
	
	IF ISNULL(@CLOCID,'')=''
		SELECT @CDEPT_ID		= [VALUE] FROM CONFIG WHERE CONFIG_OPTION='LOCATION_ID'
	ELSE
		SELECT @CDEPT_ID = @CLOCID
	
	
	DECLARE @CCONSIDER_TILL_PHY_CBS_AS_OPS VARCHAR(10)

	SELECT top 1 @CCONSIDER_TILL_PHY_CBS_AS_OPS=VALUE  FROM CONFIG WHERE CONFIG_OPTION='CONSIDER_TILL_PHY_CBS_AS_OPS'
	
	--CHECK IF ANY TILL IS OPENED BY THIS USER, IF IT IS OPEN RETURN THE SHIFT ID ELSE RETURN ERRORLOC MESSAGE.
	SELECT TOP 1 @CSHIFT_ID=SHIFT_ID,@NCOMP_OP=
	CASE WHEN ISNULL(@CCONSIDER_TILL_PHY_CBS_AS_OPS,'')='1' THEN PHY_OP ELSE COMP_OP END 
	--@NRECEIPTS=RECEIPTS,@NISSUES=ISSUES 
	FROM TILL_SHIFT_MST WHERE USER_CODE=@CUSER_CODE AND ISNULL(CLOSE_DATE,'')=''
	AND location_code= @CDEPT_ID

	IF ISNULL(@CSHIFT_ID,'')=''
		SET @CERRMSG='NO TILL IS CURRENTLY OPEN BY '+@CUSER_NAME+'.PLEASE OPEN TILL FIRST.'

	   SELECT TOP 1 @CCASHIERENABLED=VALUE FROM CONFIG WHERE CONFIG_OPTION='OPEN_CASHIER_FOR_CASHMEMOS'
	   SET @CCASHIERENABLED=ISNULL(@CCASHIERENABLED,0)


	    SELECT @NRECEIPTS=SUM(ISNULL(RECEIPTS,0))
	   FROM 
	   (
			--CASH RECEIVED THRU TILL EXPENSES
			SELECT  SUM(B.AMOUNT) AS RECEIPTS
			FROM TILL_EXPENSE_MST A
			JOIN TILL_EXPENSE_DET B ON A.MEMO_ID=B.MEMO_ID
			WHERE A.SHIFT_ID=@CSHIFT_ID AND B.XN_TYPE='CR'
			UNION ALL
			--CASH RECEIVED THRU ADVANCES/OS RECEIPTS
			SELECT SUM(B.AMOUNT) 
			FROM ARC01106 A
			JOIN PAYMODE_XN_DET B ON A.ADV_REC_ID=B.MEMO_ID AND B.XN_TYPE='ARC'
			WHERE A.SHIFT_ID=@CSHIFT_ID AND A.ARC_TYPE=1 AND B.PAYMODE_CODE='0000000'
			AND A.CANCELLED=0
			UNION ALL
			--NET CASH RECEIVED AGAINST SALES
			SELECT SUM(CASE WHEN @CCASHIERENABLED=0 THEN B.AMOUNT ELSE 0 END) 
			FROM CMM01106 A
			JOIN PAYMODE_XN_DET B ON A.CM_ID=B.MEMO_ID AND B.XN_TYPE='SLS'
			WHERE A.SHIFT_ID=@CSHIFT_ID AND B.PAYMODE_CODE='0000000'
			AND A.CANCELLED=0
			UNION ALL
			--NET CASH RECEIVED AGAINST SALES THRU CASHIER
			SELECT SUM(B.AMOUNT) 
			FROM DSM01106 A
			JOIN DSD01106 AD ON A.DS_ID=AD.DS_ID
			JOIN PAYMODE_XN_DET B ON AD.CM_ID=B.MEMO_ID AND B.XN_TYPE='SLS'
			WHERE A.SHIFT_ID=@CSHIFT_ID AND B.PAYMODE_CODE='0000000'
			AND A.CANCELLED=0
	   )A
	   




	    SELECT @NISSUES=SUM(ISSUES)
	    FROM 
	    (
			--AMOUNT ISSUED THRU TILL EXPENSES
			SELECT  SUM(B.AMOUNT) AS ISSUES 
			FROM TILL_EXPENSE_MST A
			JOIN TILL_EXPENSE_DET B ON A.MEMO_ID=B.MEMO_ID
			WHERE A.SHIFT_ID=@CSHIFT_ID AND B.XN_TYPE='DR'
			UNION ALL
			--CASH ISSUED AGAINST PAYMENTS
			SELECT SUM(B.AMOUNT) AS ISSUES 
			FROM ARC01106 A
			JOIN PAYMODE_XN_DET B ON A.ADV_REC_ID=B.MEMO_ID AND B.XN_TYPE='ARC'
			WHERE A.SHIFT_ID=@CSHIFT_ID AND A.ARC_TYPE=2 AND B.PAYMODE_CODE='0000000'
			AND A.CANCELLED=0
			UNION ALL
			--CASH ISSUES AGAINST TILL LIFTS
			SELECT SUM(A.LIFT_AMOUNT) AS ISSUES
			FROM TILL_LIFTS A 
			WHERE A.SHIFT_ID=@CSHIFT_ID AND A.CANCELLED=0
	    )B
	   
	   SET @NRECEIPTS=ISNULL(@NRECEIPTS,0)
	   SET @NISSUES=ISNULL(@NISSUES,0)


	
	SELECT ISNULL(@CSHIFT_ID,'') AS SHIFT_ID,
	ISNULL(@NCOMP_OP,0)   AS COMP_OP,
	ISNULL(@NRECEIPTS,0) AS RECEIPTS
	,ISNULL(@NISSUES,0) AS ISSUES,ISNULL(@CERRMSG,'') AS ERRMSG


END
--END OF PROCEDURE  - SP3S_GET_USERSHIFT



