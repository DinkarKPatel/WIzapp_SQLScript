
-- PROCEDURE ACT_DEPRECIATIONREPORT
-- PROCEDURE TO GET DEPRECIATION REPORT
CREATE PROCEDURE ACT_DEPRECIATIONREPORT
(
	@DFROMDT DATETIME,
	@DTODT DATETIME
)
--WITH ENCRYPTION
AS 
BEGIN
	-- SELECT @DFROMDT = '2009-04-01', @DTODT = '2010-03-31'
	DECLARE @cHEAD_CODE VARCHAR(MAX)

	DECLARE @DEP_REPORT TABLE ( AC_CODE VARCHAR(10), AC_NAME VARCHAR(100),  BILL_DATE DATETIME, REF_DATE DATETIME, 
								REF_NO VARCHAR(50), OLD_INV_AMOUNT NUMERIC(14,2), OLD_ADJ_AMOUNT NUMERIC(14,2),
								INV_AMT NUMERIC(14,2), DIFF_DAYS NUMERIC(10), DIFF_DAYS_IT_HALF NUMERIC(10),
								DIFF_DAYS_IT_FULL NUMERIC(10) )

	DECLARE @DEP_DETAIL TABLE ( AC_CODE VARCHAR(10), AC_NAME VARCHAR(100), BILL_DATE DATETIME, REF_DATE DATETIME, REF_NO VARCHAR(50), 
						 OLD_INV_AMOUNT NUMERIC(14,2), OLD_ADJ_AMOUNT NUMERIC(14,2), OLD_BAL_AMOUNT NUMERIC(14,2),
						 INV_AMT NUMERIC(14,2), ASSET_VALUE NUMERIC(14,2), DIFF_DAYS NUMERIC(10), DIFF_DAYS_IT NUMERIC(10),
						 COMPANY_ACT_PER NUMERIC(10,2), INCOME_TAX_ACT_PER NUMERIC(10,2),
						 COMPANY_ACT_DEP NUMERIC(14,2), INCOME_TAX_ACT_DEP_HALF NUMERIC(14,2),
						 INCOME_TAX_ACT_DEP_FULL NUMERIC(14,2) )

	DECLARE @LM_LIST TABLE ( AC_CODE VARCHAR(20), AC_NAME VARCHAR(100), COMPANY_ACT_PER NUMERIC(10,2), INCOME_TAX_ACT_PER NUMERIC(10,2) )


	SET @cHEAD_CODE = DBO.FN_ACT_TRAVTREE('0000000005') ---ADD VARIABLE BY GAURI ON 17/4/2019	

	INSERT @LM_LIST
	SELECT A.AC_CODE, A.AC_NAME, B.COMPANY_ACT_PER, B.INCOME_TAX_ACT_PER 
	FROM LM01106 A
	JOIN DEPD B ON A.AC_CODE = B.AC_CODE
	WHERE CHARINDEX(A.HEAD_CODE, @cHEAD_CODE) >0	----REPLACE VARIABLE FROM FUNCTION BY GAURI ON 17/4/2019 

	INSERT @DEP_REPORT ( AC_CODE, AC_NAME, BILL_DATE, REF_DATE, REF_NO, OLD_INV_AMOUNT, OLD_ADJ_AMOUNT, DIFF_DAYS, DIFF_DAYS_IT_HALF, DIFF_DAYS_IT_FULL )

	SELECT	ISNULL(F.AC_CODE,F1.AC_CODE) AS AC_CODE, 
			ISNULL(F.AC_NAME,F1.AC_NAME) AS AC_NAME, 
			ISNULL(D.VOUCHER_DT,@DFROMDT) AS BILL_DATE,
			@DFROMDT AS REF_DATE,
			'OLD' AS REF_NO,
			SUM(ISNULL(X.DR_AMOUNT,0)) + SUM(CASE WHEN A.NX_TYPE='DR' THEN A.INV_AMT ELSE 0 END) AS OLD_INV_AMOUNT, 
			SUM(ISNULL(X.CR_AMOUNT,0)) + SUM(CASE WHEN A.NX_TYPE='CR' THEN A.INV_AMT ELSE 0 END) AS OLD_ADJ_AMOUNT, 
			DATEDIFF(DD, @DFROMDT, @DTODT) + 1 AS DIFF_DAYS,
			--CAST((CASE WHEN (DATEDIFF(DD, @DFROMDT, @DTODT) + 1) < 180 THEN 180 ELSE 365 END ) AS INT) AS DIFF_DAYS_IT
			CAST((CASE WHEN (DATEDIFF(DD, @DFROMDT, @DTODT) + 1) < 180 THEN 180 ELSE 0 END ) AS INT) AS DIFF_DAYS_IT_HALF,
			CAST((CASE WHEN (DATEDIFF(DD, @DFROMDT, @DTODT) + 1) < 180 THEN 0 ELSE 365 END ) AS INT) AS DIFF_DAYS_IT_FULL
	FROM VDN01106 A   
	LEFT OUTER JOIN VD01106 C ON A.VD_ID=C.VD_ID   
	LEFT OUTER JOIN VM01106 D ON C.VM_ID=D.VM_ID   
	LEFT OUTER JOIN @LM_LIST F ON F.AC_CODE=C.AC_CODE 
	LEFT OUTER JOIN @LM_LIST F1 ON F1.AC_CODE = A.VD_ID 
	LEFT OUTER JOIN 
	( 
		SELECT	A.VDN_ID, 
				SUM(CASE WHEN A.X_TYPE='DR' THEN A.AMOUNT ELSE 0 END) AS DR_AMOUNT,
				SUM(CASE WHEN A.X_TYPE='DR' THEN 0 ELSE A.AMOUNT END) AS CR_AMOUNT
		FROM VDA01106 A 
		JOIN VD01106 B ON A.VD_ID = B.VD_ID 
		JOIN VM01106 C ON B.VM_ID = C.VM_ID 
		WHERE C.CANCELLED = 0 AND ISNULL(C.OP_ENTRY,0)=0 AND C.VOUCHER_DT< @DFROMDT
		GROUP BY A.VDN_ID
	) X ON A.VDN_ID = X.VDN_ID 
	WHERE A.REF_DATE < @DFROMDT
	AND ISNULL(D.CANCELLED,0) = 0 
	AND A.INV_AMT <> 0
	AND ISNULL(F.AC_NAME,F1.AC_NAME) IS NOT NULL
	GROUP BY ISNULL(F.AC_CODE,F1.AC_CODE), ISNULL(F.AC_NAME,F1.AC_NAME), ISNULL(D.VOUCHER_DT,@DFROMDT)
	ORDER BY AC_NAME

	INSERT @DEP_REPORT ( AC_CODE, AC_NAME, BILL_DATE, REF_DATE, REF_NO, INV_AMT, DIFF_DAYS, DIFF_DAYS_IT_HALF, DIFF_DAYS_IT_FULL )
	SELECT	ISNULL(F.AC_CODE,F1.AC_CODE) AS AC_CODE, 
			ISNULL(F.AC_NAME,F1.AC_NAME) AS AC_NAME, 
			ISNULL(D.VOUCHER_DT, A.REF_DATE) AS BILL_DATE,
			A.REF_DATE, 
			A.REF_NO, 
			(CASE WHEN A.NX_TYPE='DR' THEN A.INV_AMT ELSE 0 END) AS INV_AMT, 
			DATEDIFF(DD, A.REF_DATE, @DTODT) + 1 AS DIFF_DAYS,
			--CAST((CASE WHEN (DATEDIFF(DD, A.REF_DATE, @DTODT) + 1) < 180 THEN 180 ELSE 365 END ) AS INT) AS DIFF_DAYS_IT
			CAST((CASE WHEN (DATEDIFF(DD, A.REF_DATE, @DTODT) + 1) < 180 THEN 180 ELSE 0 END ) AS INT) AS DIFF_DAYS_IT_HALF,
			CAST((CASE WHEN (DATEDIFF(DD, A.REF_DATE, @DTODT) + 1) < 180 THEN 0 ELSE 365 END ) AS INT) AS DIFF_DAYS_IT_FULL
	FROM VDN01106 A   
	LEFT OUTER JOIN VD01106 C ON A.VD_ID=C.VD_ID   
	LEFT OUTER JOIN VM01106 D ON C.VM_ID=D.VM_ID   
	LEFT OUTER JOIN @LM_LIST F ON F.AC_CODE=C.AC_CODE 
	LEFT OUTER JOIN @LM_LIST F1 ON F1.AC_CODE = A.VD_ID 
	WHERE A.REF_DATE BETWEEN @DFROMDT AND @DTODT
	AND ISNULL(D.CANCELLED,0) = 0 
	AND A.INV_AMT <> 0
	AND A.NX_TYPE='DR'
	AND ISNULL(F.AC_NAME,F1.AC_NAME) IS NOT NULL
	ORDER BY AC_NAME,A.REF_DATE, A.REF_NO 

	-- SELECT * FROM @DEP_REPORT

	INSERT @DEP_DETAIL ( AC_CODE, AC_NAME, BILL_DATE, REF_DATE, REF_NO, 
						 OLD_INV_AMOUNT, OLD_ADJ_AMOUNT, OLD_BAL_AMOUNT,
						 INV_AMT, ASSET_VALUE, DIFF_DAYS, DIFF_DAYS_IT, COMPANY_ACT_PER, INCOME_TAX_ACT_PER,
						 COMPANY_ACT_DEP, INCOME_TAX_ACT_DEP_HALF, INCOME_TAX_ACT_DEP_FULL )
	SELECT	A.AC_CODE, A.AC_NAME, BILL_DATE, REF_DATE, REF_NO, 
			ISNULL(OLD_INV_AMOUNT,0) AS OLD_INV_AMOUNT, ISNULL(OLD_ADJ_AMOUNT,0) AS OLD_ADJ_AMOUNT, 
			(ISNULL(OLD_INV_AMOUNT,0) - ISNULL(OLD_ADJ_AMOUNT,0)) AS OLD_BAL_AMOUNT,
			ISNULL(INV_AMT,0) AS INV_AMT, 
			(ISNULL(OLD_INV_AMOUNT,0) - ISNULL(OLD_ADJ_AMOUNT,0) + ISNULL(INV_AMT,0)) AS ASSET_VALUE,
			DIFF_DAYS, 
			(DIFF_DAYS_IT_HALF + DIFF_DAYS_IT_FULL) AS DIFF_DAYS_IT,
			B.COMPANY_ACT_PER, B.INCOME_TAX_ACT_PER, 
			ROUND(ABS((((ISNULL(OLD_INV_AMOUNT,0) - ISNULL(OLD_ADJ_AMOUNT,0) + ISNULL(INV_AMT,0))*B.COMPANY_ACT_PER/100)/365)*DIFF_DAYS),2) AS COMPANY_ACT_DEP, 
			--ROUND(ABS((((ISNULL(OLD_INV_AMOUNT,0) - ISNULL(OLD_ADJ_AMOUNT,0) + ISNULL(INV_AMT,0))*B.INCOME_TAX_ACT_PER/100)/365)*DIFF_DAYS_IT),2) AS INCOME_TAX_ACT_DEP
			ROUND(ABS((((ISNULL(OLD_INV_AMOUNT,0) - ISNULL(OLD_ADJ_AMOUNT,0) + ISNULL(INV_AMT,0))*B.INCOME_TAX_ACT_PER/100)/365)*DIFF_DAYS_IT_HALF),2) AS INCOME_TAX_ACT_DEP_HALF,
			ROUND(ABS((((ISNULL(OLD_INV_AMOUNT,0) - ISNULL(OLD_ADJ_AMOUNT,0) + ISNULL(INV_AMT,0))*B.INCOME_TAX_ACT_PER/100)/365)*DIFF_DAYS_IT_FULL),2) AS INCOME_TAX_ACT_DEP_FULL
	FROM @DEP_REPORT A 
	JOIN @LM_LIST B ON A.AC_CODE = B.AC_CODE
	ORDER BY A.AC_NAME, A.REF_DATE

	-- DEPRECIATION SUMMARY REPORT AS PER COMPANY ACT
	SELECT	AC_NAME, SUM(OLD_INV_AMOUNT) AS OLD_ASSET_VALUE, 
			SUM(INV_AMT) AS NEW_ASSET_VALUE, 
			SUM(OLD_BAL_AMOUNT + INV_AMT) AS TOTAL_ASSET_VALUE,
			SUM(OLD_ADJ_AMOUNT) AS OLD_DEP_AMOUNT, 
			SUM(COMPANY_ACT_DEP) AS NEW_DEP_AMOUNT,
			SUM(OLD_ADJ_AMOUNT + COMPANY_ACT_DEP) AS TOTAL_DEP_AMOUNT,
			SUM((OLD_BAL_AMOUNT + INV_AMT) - (OLD_ADJ_AMOUNT + COMPANY_ACT_DEP)) AS NEW_WDV_AMOUNT,
			SUM(OLD_INV_AMOUNT - OLD_ADJ_AMOUNT) AS OLD_WDV_AMOUNT
	FROM @DEP_DETAIL
	GROUP BY AC_NAME
	ORDER BY AC_NAME

	-- DEPRECIATION DETAILED REPORT AS PER COMPANY ACT
	SELECT	AC_CODE, AC_NAME,  BILL_DATE, REF_NO, REF_DATE, OLD_INV_AMOUNT, OLD_ADJ_AMOUNT, OLD_BAL_AMOUNT,
			INV_AMT, ASSET_VALUE, DIFF_DAYS, COMPANY_ACT_PER AS DEP_PER, COMPANY_ACT_DEP AS DEP_AMT,
			(OLD_ADJ_AMOUNT + COMPANY_ACT_DEP ) AS TOTAL_DEP_AMT,
			((OLD_BAL_AMOUNT + INV_AMT) - (OLD_ADJ_AMOUNT + COMPANY_ACT_DEP)) AS NEW_WDV_AMOUNT
	FROM @DEP_DETAIL 
	ORDER BY AC_NAME, REF_DATE, REF_NO

	-- DEPRECIATION SUMMARY REPORT AS PER INCOMETAX ACT
	SELECT	AC_NAME, SUM(OLD_INV_AMOUNT) AS OLD_ASSET_VALUE, 
			SUM((CASE WHEN INCOME_TAX_ACT_DEP_FULL=0 THEN INV_AMT ELSE 0 END)) AS NEW_ASSET_VALUE_FULL, 
			SUM((CASE WHEN INCOME_TAX_ACT_DEP_HALF=0 THEN INV_AMT ELSE 0 END)) AS NEW_ASSET_VALUE_HALF, 
			SUM(OLD_BAL_AMOUNT + INV_AMT) AS TOTAL_ASSET_VALUE,

			SUM(INCOME_TAX_ACT_DEP_FULL) AS NEW_DEP_AMOUNT_FULL,
			SUM(INCOME_TAX_ACT_DEP_HALF) AS NEW_DEP_AMOUNT_HALF,
			SUM(INCOME_TAX_ACT_DEP_HALF + INCOME_TAX_ACT_DEP_FULL) AS NEW_DEP_AMOUNT,

			SUM((OLD_BAL_AMOUNT + INV_AMT) - (OLD_ADJ_AMOUNT + (INCOME_TAX_ACT_DEP_HALF + INCOME_TAX_ACT_DEP_FULL))) AS NEW_WDV_AMOUNT
	FROM @DEP_DETAIL
	GROUP BY AC_NAME
	ORDER BY AC_NAME

	-- DEPRECIATION DETAILED REPORT AS PER INCOME TAX ACT
	SELECT	AC_CODE, AC_NAME, BILL_DATE, REF_NO, REF_DATE,
			-- OLD_INV_AMOUNT, 
			-- OLD_ADJ_AMOUNT, 
			OLD_BAL_AMOUNT,
			INV_AMT, ASSET_VALUE, 
			DIFF_DAYS_IT AS DIFF_DAYS, 
			INCOME_TAX_ACT_PER AS DEP_PER,
			INCOME_TAX_ACT_DEP_FULL AS DEP_AMT_FULL,
			INCOME_TAX_ACT_DEP_HALF AS DEP_AMT_HALF,
			( INCOME_TAX_ACT_DEP_HALF + INCOME_TAX_ACT_DEP_FULL ) AS TOTAL_DEP_AMT,
			(ASSET_VALUE - ( INCOME_TAX_ACT_DEP_HALF + INCOME_TAX_ACT_DEP_FULL )) AS NEW_WDV_AMT
	FROM @DEP_DETAIL 
	ORDER BY AC_NAME, REF_DATE, REF_NO
END
