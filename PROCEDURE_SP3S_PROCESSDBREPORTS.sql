CREATE PROCEDURE SP3S_PROCESSDBREPORTS
(
	 @DTODATE DATETIME = ''		 ---PROCESS DATE
	,@CREPID VARCHAR(20)=''		 ---REPORT ID
	,@CREPORT_TYPE VARCHAR(4)='' ---REPORT TYPE
	,@BPROCESSALLREPORTS BIT=1   ---1 : ALL MGDB REPORTS FOR ALL MONTHS WILL BE PROCESSED
)	
--WITH ENCRYPTION
AS
BEGIN
SET NOCOUNT ON
/*
	***THIS PROCEDURE PREPARES MANAGEMENT DASHBOARD DATA AND STORES IT IN A TABLE.
	***EITHER A REPORT CAN BE PROCESSED FOR A DATE OR ALL REPORTS CAN BE PROCESSED AT ALL DATES
	***ALL DATES: CURRENT DATE AND LAST DAY DATE FOR ALL PREVIOUS MONTHS IN THE CURRENT FIN_YEAR
	***THIS PROCEDURE FURTHER CALLS SP3S_GEN_MGDBDATA FOR EACH REPORT PROCESSING
*/
	DECLARE  @CTODATE VARCHAR(20)
			,@CLAYOUT_COL VARCHAR(500) 
			,@CLAYOUT_DSP VARCHAR(500) 
			,@CFILTER_DEF VARCHAR(MAX)
			,@CAGE1 VARCHAR(10)
			,@CAGE2 VARCHAR(10)
			,@CAGE3 VARCHAR(10)
			,@CNEWPROCESSID VARCHAR(50)
			,@CLOCATIONID VARCHAR(5)
			,@NDAYS NUMERIC(4) 
			,@CERRMSG VARCHAR(MAX)
			,@CSTEP VARCHAR(10)
				
	IF OBJECT_ID('TEMPDB..#PURCHASEVALUE','U') IS NOT NULL
		DROP TABLE #PURCHASEVALUE
	
	CREATE TABLE #PURCHASEVALUE
	(
	   LAYOUTCOL VARCHAR(1000) NOT NULL DEFAULT 0
	  ,PURCHASE_VALUE NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,PUR_CNTR NUMERIC(10,2) NOT NULL DEFAULT 0	
	  ,SALE_VALUE NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,SOLD_PURCHASE_VALUE NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,SALE_DAYS NUMERIC(4,0) NOT NULL DEFAULT 0
	  ,AVG_DAILY_SALE NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,CBS_VALUE_PP NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,DIH NUMERIC(10) NOT NULL DEFAULT 0
	  ,SALE_CNTR NUMERIC(10,2) NOT NULL DEFAULT 0
	  ,GROSS_MARGIN NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,GM_CNTR NUMERIC(10,2) NOT NULL DEFAULT 0
	  ,ADI NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,TPY NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,PSPD NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,GMROI NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,ASD NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,AGE1 NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,AGE2 NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,AGE3 NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,AGE4 NUMERIC(18,2) NOT NULL DEFAULT 0
	  ,DAYS NUMERIC(4,0) NOT NULL DEFAULT 0
	  
	 )
		----IF NO PROCESSING DATE IS PASSED,SELECT CURRENT SYSTEM DATE 
		IF @DTODATE=''
			SELECT TOP 1 @DTODATE=dbo.fn_getlastrundate()
	
		IF ISNULL(@DTODATE,'')=''
			SET @DTODATE=GETDATE()
		
		SELECT TOP 1 @CLOCATIONID=VALUE FROM CONFIG WHERE CONFIG_OPTION='LOCATION_ID' 
		
		--FETCHING AGE VALUES	
		SELECT @CAGE1=VALUE FROM CONFIG WHERE CONFIG_OPTION='AGEING_1'
		SELECT @CAGE2=VALUE FROM CONFIG WHERE CONFIG_OPTION='AGEING_2'
		SELECT @CAGE3=VALUE FROM CONFIG WHERE CONFIG_OPTION='AGEING_3'
	
		--IF NO VALUE IS DEFINED IN THE CONFIG, SET THE DEFAULT VAULES
		IF ISNULL(@CAGE1,'')=''
			SET @CAGE1=30
		
		IF ISNULL(@CAGE2,'')=''
			SET @CAGE2=60
			
		IF ISNULL(@CAGE3,'')=''
			SET @CAGE3=90		
	
		SET @DTODATE=DATEADD(DD,0,DATEDIFF(DD,0,@DTODATE))
		SET @CTODATE=CONVERT(VARCHAR,@DTODATE,110)

IF (@CREPID<>'' AND @CREPORT_TYPE<>'')
	GOTO PROCESS_A_REPORT
ELSE	
	GOTO PROCESS_ALL_REPORTS
		
PROCESS_A_REPORT:
	BEGIN TRY
		---PROCESS A MANAGEMENT DASHBOARD REPORTS
		--SELECT @CLAYOUT_COL=LAYOUT,@CFILTER_DEF=FILTER,@CLAYOUT_DSP=LAYOUT_DISPLAY
		--FROM MGMT_REPS WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE 
		SELECT @CLAYOUT_COL=LAYOUT,@CFILTER_DEF=FILTER,@CLAYOUT_DSP=LAYOUT_DISPLAY FROM MGMT_REPS WHERE  
		REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE 
		
		IF ISNULL(@CLAYOUT_COL,'')=''
		BEGIN
			SET @CERRMSG='ERROR EXECUTING PROCEDURE - SP3S_PROCESSDBREPORTS, AT STEP '+ISNULL(@CSTEP,'')+', MESSAGE - PLEASE DEFINE LAYOUT.'	
			GOTO EXIT_PROC
		END
		
		TRUNCATE TABLE #PURCHASEVALUE
		EXEC SP3S_GEN_MGDBDATA @DTODATE,@CAGE1,@CAGE2,@CAGE3,@CFILTER_DEF,@CLAYOUT_COL
				
				---CHECK IF THE REPORT HAS BEEN PROCESSED BEFORE 
				---IF YES, DELETE THE REPORT 
				---INSERT THE DATA FOR THE REPORT
				IF EXISTS(SELECT PROCESSID FROM MGMT_DB_MST WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE AND PROCESSDT=@CTODATE)
				BEGIN
					DELETE FROM MGMT_DB_DET WHERE PROCESSID IN 
					(SELECT PROCESSID FROM MGMT_DB_MST WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE AND PROCESSDT=@CTODATE)
					
					DELETE FROM MGMT_DB_MST WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE AND PROCESSDT=@CTODATE
				END
		
		SET @CNEWPROCESSID=@CLOCATIONID+CONVERT(VARCHAR(40),NEWID())		
		
		SET @NDAYS=0
		SELECT TOP 1 @NDAYS=DAYS FROM #PURCHASEVALUE
		
		INSERT MGMT_DB_MST	( PROCESSID, PROCESSDT, REP_ID, REPORT_TYPE, LAYOUT_DEF
							, LAYOUT_DISPLAY, FILTER_DEF, AGE1_DEF, AGE2_DEF, AGE3_DEF 
							, DAYS )  
		SELECT 	 @CNEWPROCESSID AS PROCESSID,@CTODATE AS PROCESSDT,@CREPID AS REP_ID
				,@CREPORT_TYPE AS REPORT_TYPE,@CLAYOUT_COL AS LAYOUT_DEF,@CLAYOUT_DSP AS LAYOUT_DISPLAY
				,@CFILTER_DEF AS FILTER_DEF,@CAGE1 AS AGE1_DEF,@CAGE2 AS AGE2_DEF,@CAGE3 AS AGE3_DEF 
				,@NDAYS AS DAYS
				
		INSERT MGMT_DB_DET	( PROCESSID, LAYOUTCOL, PURCHASE_VALUE, PUR_CNTR, SALE_VALUE
							, SOLD_PURCHASE_VALUE, SALE_DAYS, AVG_DAILY_SALE, CBS_VALUE_PP
							, DIH, SALE_CNTR, GROSS_MARGIN, GM_CNTR, ADI, TPY,PSPD,GMROI, ASD, AGE1
							, AGE2, AGE3, AGE4 )  
		SELECT 	@CNEWPROCESSID AS PROCESSID, LAYOUTCOL, PURCHASE_VALUE, PUR_CNTR, SALE_VALUE
			  , SOLD_PURCHASE_VALUE, SALE_DAYS, AVG_DAILY_SALE, CBS_VALUE_PP, DIH, SALE_CNTR
			  , GROSS_MARGIN, GM_CNTR, ADI, TPY,PSPD, GMROI, ASD, AGE1, AGE2, AGE3, AGE4 
		FROM #PURCHASEVALUE
	END TRY
	BEGIN CATCH
		SET @CERRMSG='ERROR EXECUTING PROCEDURE - SP3S_PROCESSDBREPORTS, AT STEP '+ISNULL(@CSTEP,'')+', MESSAGE : '+ERROR_MESSAGE()
		GOTO EXIT_PROC
	END CATCH
GOTO EXIT_PROC

PROCESS_ALL_REPORTS:		
	BEGIN TRY
		IF OBJECT_ID('TEMPDB..#MGDATES','U') IS NOT NULL
				DROP TABLE #MGDATES
		CREATE TABLE #MGDATES(PROCESSDATES DATETIME)
		
		INSERT #MGDATES(PROCESSDATES)
		SELECT @DTODATE
		
		IF @BPROCESSALLREPORTS=1
		BEGIN
			---GETTING ALL DATES FOR PROCESSING
			DECLARE @CFINYEAR VARCHAR(5)
				   ,@DFYSDATE DATETIME
				   ,@NTOMONTH NUMERIC(2)
				   ,@NTOYEAR NUMERIC(4)
			
			SELECT @NTOMONTH=MONTH(@DTODATE)
			SELECT @NTOYEAR=YEAR(@DTODATE)
			
			IF @NTOMONTH<>4
			BEGIN
				SELECT @CFINYEAR='01'+DBO.FN_GETFINYEAR(@DTODATE)
				SELECT @DFYSDATE=DBO.FN_GETFINYEARDATE(@CFINYEAR,'1')
				
				IF @NTOMONTH<4
				BEGIN
					---PROCESS MONTHS : 3,2,1,12,11,10,9,8,7,6,5
					WHILE @NTOMONTH>1
					BEGIN
						SET @NTOMONTH=@NTOMONTH-1
						
						INSERT #MGDATES(PROCESSDATES)
						SELECT DBO.FN_LAST_DATE(@NTOMONTH,@NTOYEAR)
					END
					SET @NTOMONTH=12
					SET @NTOYEAR=@NTOYEAR-1
				END
				ELSE
					SET @NTOMONTH=@NTOMONTH-1
				
					WHILE @NTOMONTH>3
					BEGIN
						INSERT #MGDATES(PROCESSDATES)
						SELECT DBO.FN_LAST_DATE(@NTOMONTH,@NTOYEAR)
						
						SET @NTOMONTH=@NTOMONTH-1	
					END
			END				
		END
		
		UPDATE #MGDATES SET PROCESSDATES=DATEADD(DD,0,DATEDIFF(DD,0,PROCESSDATES))
		
		---CURSOR TO POINT TO ALL PROCESSDATES ONE BY ONE
		IF CURSOR_STATUS('GLOBAL','CURDATES') IN (0,1)
		BEGIN
			CLOSE CURDATES
			DEALLOCATE CURDATES
		END
		DECLARE CURDATES CURSOR FOR SELECT PROCESSDATES FROM #MGDATES
		OPEN CURDATES
		FETCH NEXT FROM CURDATES INTO @DTODATE
		WHILE @@FETCH_STATUS=0
		BEGIN
			SET @CTODATE=CONVERT(VARCHAR,@DTODATE,110)
			---CURSOR TO PROCESS ALL MANAGEMENT DASHBOARD REPORTS
			IF CURSOR_STATUS('GLOBAL','CURREPORTS') IN (0,1)
			BEGIN
				CLOSE CURREPORTS
				DEALLOCATE CURREPORTS
			END
			
			DECLARE CURREPORTS CURSOR FOR 
			SELECT LAYOUT,FILTER,LAYOUT_DISPLAY,REP_ID,REPORT_TYPE FROM MGMT_REPS WHERE REPORT_TYPE=2
			OPEN CURREPORTS
			FETCH NEXT FROM CURREPORTS INTO @CLAYOUT_COL,@CFILTER_DEF,@CLAYOUT_DSP,@CREPID,@CREPORT_TYPE
			WHILE @@FETCH_STATUS=0
			BEGIN
					IF ISNULL(@CLAYOUT_COL,'')=''
						GOTO NEXTRPT
					
					TRUNCATE TABLE #PURCHASEVALUE
					EXEC SP3S_GEN_MGDBDATA @DTODATE,@CAGE1,@CAGE2,@CAGE3,@CFILTER_DEF,@CLAYOUT_COL
					
					---CHECK IF THE REPORT HAS BEEN PROCESSED BEFORE 
					---IF YES, DELETE THE REPORT 
					---INSERT THE DATA FOR THE REPORT
					IF EXISTS(SELECT PROCESSID FROM MGMT_DB_MST WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE AND PROCESSDT=@CTODATE)
					BEGIN
						DELETE FROM MGMT_DB_DET WHERE PROCESSID IN 
						(SELECT PROCESSID FROM MGMT_DB_MST WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE AND PROCESSDT=@CTODATE)
						
						DELETE FROM MGMT_DB_MST WHERE REP_ID=@CREPID AND REPORT_TYPE=@CREPORT_TYPE AND PROCESSDT=@CTODATE
					END
			
			SET @CNEWPROCESSID=@CLOCATIONID+CONVERT(VARCHAR(40),NEWID())		
			
			SET @NDAYS=0
			SELECT TOP 1 @NDAYS=DAYS FROM #PURCHASEVALUE
					
			INSERT MGMT_DB_MST	( PROCESSID, PROCESSDT, REP_ID, REPORT_TYPE, LAYOUT_DEF
								, LAYOUT_DISPLAY, FILTER_DEF, AGE1_DEF, AGE2_DEF, AGE3_DEF,DAYS )  
			SELECT 	 @CNEWPROCESSID AS PROCESSID,@CTODATE AS PROCESSDT,@CREPID AS REP_ID
					,@CREPORT_TYPE AS REPORT_TYPE,@CLAYOUT_COL AS LAYOUT_DEF,@CLAYOUT_DSP AS LAYOUT_DISPLAY
					,@CFILTER_DEF AS FILTER_DEF,@CAGE1 AS AGE1_DEF,@CAGE2 AS AGE2_DEF,@CAGE3 AS AGE3_DEF 
					,@NDAYS AS DAYS
					
			INSERT MGMT_DB_DET	( PROCESSID, LAYOUTCOL, PURCHASE_VALUE, PUR_CNTR, SALE_VALUE
								, SOLD_PURCHASE_VALUE, SALE_DAYS, AVG_DAILY_SALE, CBS_VALUE_PP
								, DIH, SALE_CNTR, GROSS_MARGIN, GM_CNTR, ADI, TPY,PSPD, GMROI, ASD, AGE1
								, AGE2, AGE3, AGE4 )  
			SELECT 	@CNEWPROCESSID AS PROCESSID, LAYOUTCOL, PURCHASE_VALUE, PUR_CNTR, SALE_VALUE
				  , SOLD_PURCHASE_VALUE, SALE_DAYS, AVG_DAILY_SALE, CBS_VALUE_PP, DIH, SALE_CNTR
				  , GROSS_MARGIN, GM_CNTR, ADI, TPY,PSPD, GMROI, ASD, AGE1, AGE2, AGE3, AGE4 
			FROM #PURCHASEVALUE
			NEXTRPT:					
			FETCH NEXT FROM CURREPORTS INTO @CLAYOUT_COL,@CFILTER_DEF,@CLAYOUT_DSP,@CREPID,@CREPORT_TYPE
			END
			CLOSE CURREPORTS
			DEALLOCATE CURREPORTS
			---CURSOR TO PROCESS ALL MANAGEMENT DASHBOARD REPORTS
		FETCH NEXT FROM CURDATES INTO @DTODATE
		END
		CLOSE CURDATES
		DEALLOCATE CURDATES
	END TRY
	BEGIN CATCH
		SET @CERRMSG='ERROR EXECUTING PROCEDURE - SP3S_PROCESSDBREPORTS, AT STEP '+ISNULL(@CSTEP,'')+', MESSAGE : '+ERROR_MESSAGE()
		GOTO EXIT_PROC
	END CATCH
GOTO EXIT_PROC

EXIT_PROC:	
	IF ISNULL(@CERRMSG,'')<>''
		SELECT @CERRMSG AS ERRMSG 
END
---END OF PROCEDURE - SP3S_PROCESSDBREPORTS
