CREATE PROCEDURE SP8_CRT_OPVOUCHERS
(
	@FIN_YEAR VARCHAR(5)
	/*END FIN_YEAR TILL WHERE ENTRIES TO BE CONSIDERED*/
)
--WITH ENCRYPTION
AS
BEGIN
/*
	THIS PROCEDURE WILL CREATE OPENING BALANCES VOUCHER ENTRIES FOR PASSED FIN_YEAR FOR ALL LEDGERS WITH BILL_BY_BILL FLAG ENABLED IN RESPECTIVE TABLES PREFIXED WITH 
	'_' 
*/	
SET NOCOUNT ON
DECLARE @TILLDATE DATETIME

----CREATING TARGET TABLES
IF OBJECT_ID('_VDN01106','U') IS NOT NULL
	DROP TABLE _VDN01106	
	
IF OBJECT_ID('_LOCOB','U') IS NOT NULL
	DROP TABLE _LOCOB		
	
IF OBJECT_ID('_LOCOB','U') IS NOT NULL
	DROP TABLE _LOCOB

IF OBJECT_ID('TEMPDB..#HEADS','U') IS NOT NULL
	DROP TABLE #HEADS

CREATE TABLE #HEADS(HEAD_CODE VARCHAR(10))	

INSERT #HEADS(HEAD_CODE)
SELECT * FROM DBO.FN3S_SUBHEADS('0000000010')
UNION ALL
SELECT * FROM DBO.FN3S_SUBHEADS('0000000017')

IF EXISTS(SELECT TOP 1 VALUE FROM CONFIG WHERE CONFIG_OPTION='PICK_PROFILTLOSS_HEADS' AND VALUE<>1)
BEGIN
	INSERT #HEADS(HEAD_CODE)
	SELECT * FROM DBO.FN3S_SUBHEADS('0000000009')		
END

SELECT @TILLDATE=DBO.FN_GETFINYEARDATE(@FIN_YEAR,1)

SELECT * INTO _VDN01106 FROM VDN01106 WHERE 1=2
SELECT * INTO _LOCOB FROM LOCOB WHERE 1=2

	--BILL BY BILL PENDENCY
	INSERT _VDN01106	( VD_ID, VDN_ID, REF_NO, REF_DATE, CR_DAYS, INV_AMT, BROKER_AC_CODE, OB, LAST_UPDATE, NX_TYPE, COMPANY_CODE, FIN_YEAR, DISCOUNT_PERCENTAGE, ON_ACCOUNT, DEPT_ID )  
	SELECT 	 VD.AC_CODE AS VD_ID, VDN.VDN_ID, VDN.REF_NO, @TILLDATE AS REF_DATE, VDN.CR_DAYS, (VDN.INV_AMT-ISNULL(ADJ.AMOUNT,0)) AS INV_AMT, VDN.BROKER_AC_CODE, VDN.OB, @TILLDATE AS LAST_UPDATE, VDN.NX_TYPE, VDN.COMPANY_CODE,@FIN_YEAR AS FIN_YEAR, VDN.DISCOUNT_PERCENTAGE, VDN.ON_ACCOUNT, VDN.DEPT_ID 
	FROM VDN01106 VDN
	LEFT JOIN VD01106 VD ON VDN.VD_ID=VD.VD_ID
	LEFT JOIN VM01106 VM ON VD.VM_ID=VM.VM_ID
	LEFT JOIN LM01106 LM ON LM.AC_CODE=VD.AC_CODE
	LEFT JOIN 
	(
		SELECT VDA.VDN_ID,SUM(VDA.AMOUNT) AS AMOUNT
		FROM VDA01106 VDA
		JOIN VD01106 VD ON VDA.VD_ID=VD.VD_ID
		JOIN VM01106 VM ON VD.VM_ID=VM.VM_ID
		WHERE VM.CANCELLED=0 AND VM.VOUCHER_DT<@TILLDATE
		GROUP BY VDA.VDN_ID
	) ADJ ON VDN.VDN_ID=ADJ.VDN_ID
	WHERE ((VM.VM_ID IS NOT NULL AND VM.CANCELLED=0) OR VM.VM_ID IS NULL)
	AND (VDN.INV_AMT-ISNULL(ADJ.AMOUNT,0))>0
	AND VDN.REF_DATE<@TILLDATE AND LM.HEAD_CODE NOT IN
	(SELECT HEAD_CODE FROM #HEADS)
	
	--ON ACCOUNT PENDENCY
	IF OBJECT_ID('TEMPDB..#VD','U') IS NOT NULL
		DROP TABLE #VD
	
	SELECT VD.*,VM.DEPT_ID INTO #VD 
	FROM VD01106 VD 
	JOIN VM01106 VM ON VD.VM_ID=VM.VM_ID 
	LEFT JOIN LM01106 LM ON VD.AC_CODE=LM.AC_CODE 	
	LEFT JOIN LMP01106 LMP ON VD.AC_CODE=LMP.AC_CODE 	
	WHERE VM.CANCELLED=0 AND (LMP.AC_CODE IS NULL OR LMP.BILL_BY_BILL=0) AND VM.VOUCHER_DT<@TILLDATE	
	AND LM.HEAD_CODE NOT IN
	(SELECT HEAD_CODE FROM #HEADS)
	
	INSERT _LOCOB	( AC_CODE, OPENING_BALANCE, OPENING_BALANCE_CR_DR, DEPT_ID, COMPANY_CODE, LAST_UPDATE, ROW_ID, CLOSING_BALANCE, CLOSING_BALANCE_CR_DR, FIN_YEAR )  
	SELECT AC_CODE
	,(CASE  WHEN SUM(DEBIT_AMOUNT)>SUM(CREDIT_AMOUNT) THEN SUM(DEBIT_AMOUNT)-SUM(CREDIT_AMOUNT) 
			WHEN SUM(CREDIT_AMOUNT)>SUM(DEBIT_AMOUNT) THEN SUM(CREDIT_AMOUNT)-SUM(DEBIT_AMOUNT) 
	  END)	AS OPENING_BALANCE
	,(CASE  WHEN SUM(DEBIT_AMOUNT)>SUM(CREDIT_AMOUNT) THEN 'DR' 
			WHEN SUM(CREDIT_AMOUNT)>SUM(DEBIT_AMOUNT) THEN 'CR'
	  END)	AS OPENING_BALANCE_CR_DR
	,DEPT_ID     	
	,'01' AS  COMPANY_CODE
	,@TILLDATE AS LAST_UPDATE
	,'01'+CONVERT(VARCHAR(40),NEWID()) AS ROW_ID
	,0 AS  CLOSING_BALANCE
	,'' AS CLOSING_BALANCE_CR_DT
	,@FIN_YEAR AS FIN_YEAR
	FROM #VD
	GROUP BY AC_CODE,DEPT_ID
	HAVING SUM(DEBIT_AMOUNT)<>SUM(CREDIT_AMOUNT)
	
	PRINT 'OPENING BALANCE INFORMATION: '+CHAR(13)+CHAR(13)+'CHECK _VDN01106 TABLE FOR BILL_BY_BILL VOUCHERS OPENING BALANCE ,'+CHAR(13)+'AND _LOCOB TABLE FOR ON ACCOUNTS OPENING BALANCE'
END
