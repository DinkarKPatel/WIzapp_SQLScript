CREATE PROCEDURE SP3S_CHECK_PENDING_BILLS--(LocId 3 digit change by Sanjay:06-11-2024)
(
	 @DFROM_DT DATETIME
	,@DTO_DT DATETIME
	,@CDEPT_ID VARCHAR(4)
	,@BESTIMATEENABLED BIT
	,@CUSERCODE	VARCHAR(15)
	,@CBINID	VARCHAR(10)
	,@CERRMSGOUT VARCHAR(500) OUTPUT
)
--WITH ENCRYPTION
AS
BEGIN
	IF OBJECT_ID('TEMPDB..#PENDINGBILLS','U') IS NOT NULL	
		DROP TABLE #PENDINGBILLS

	CREATE TABLE #PENDINGBILLS(CM_ID VARCHAR(22))
	
	DECLARE @BDSM BIT
	
	SELECT TOP 1 @BDSM=VALUE FROM CONFIG WHERE CONFIG_OPTION='OPEN_CASHIER_FOR_CASHMEMOS'
	SET @BDSM=ISNULL(@BDSM,0)	

	IF @BDSM=1
	BEGIN
		INSERT #PENDINGBILLS(CM_ID)
		SELECT A.CM_ID 
		FROM  CMM01106  A (NOLOCK) 
		JOIN 
		(
			 SELECT MEMO_ID,SUM(CASE WHEN PAYMODE_GRP_CODE='0000001' THEN AMOUNT ELSE 0 END) AS CASH_AMOUNT,
			 SUM(CASE WHEN A.PAYMODE_CODE IN ('0000004','CMR0001')  THEN AMOUNT ELSE 0 END) AS CREDIT_AMOUNT
			 FROM PAYMODE_XN_DET A
			 JOIN PAYMODE_MST B ON A.PAYMODE_CODE=B.PAYMODE_CODE
			 WHERE XN_TYPE='SLS' GROUP BY MEMO_ID
		) PAY ON PAY.MEMO_ID=A.CM_ID 
		LEFT JOIN 
		(
			SELECT CM_ID FROM DSD01106 DSD(NOLOCK) JOIN DSM01106 DSM(NOLOCK) ON DSD.DS_ID=DSM.DS_ID
			WHERE DSM.CANCELLED=0
		)DS ON A.CM_ID=DS.CM_ID
		WHERE A.CM_DT BETWEEN @DFROM_DT AND @DTO_DT
		AND (@CDEPT_ID='' OR A.location_Code=@CDEPT_ID) 
		AND (@CUSERCODE='' OR A.USER_CODE=@CUSERCODE)
		AND (A.MEMO_TYPE=1 OR @BESTIMATEENABLED=1)
		AND (@CBINID='' OR A.BIN_ID=@CBINID)
		AND NOT(A.NET_AMOUNT<0 AND PAY.CREDIT_AMOUNT<>0  AND PAY.CASH_AMOUNT=0)/*EXCLUDING CREDITNOTE AND CREDIT REFUND BILLS*/
		AND A.CANCELLED = 0 
		AND  DS.CM_ID IS NULL 
		
		
		IF EXISTS(SELECT TOP 1 'U' FROM #PENDINGBILLS)
		BEGIN
			SET @CERRMSGOUT='REPORT NOT FINAL AS SOME BILLS ARE PENDING FOR SETTLEMENT BY CASHIER'
		END
	END	
	ELSE
	BEGIN
		---ON CANCELLATION OF CASH MEMO, THE REFERENCE OF CASHMEMO WITH PACKSLIP SHOULD BE DELETED
		INSERT #PENDINGBILLS(CM_ID)
		SELECT A.CM_ID
		FROM RPS_MST A
		WHERE ISNULL(A.REF_CM_ID,'')='' AND A.CANCELLED=0
		AND A.CM_DT BETWEEN @DFROM_DT AND @DTO_DT
		AND (@CDEPT_ID='' OR a.location_Code=@CDEPT_ID)
		AND (@CUSERCODE='' OR A.USER_CODE=@CUSERCODE)
		AND (@CBINID='' OR A.BIN_ID=@CBINID)
		
		IF EXISTS(SELECT TOP 1 'U' FROM #PENDINGBILLS)
		BEGIN
			SET @CERRMSGOUT='REPORT NOT FINAL AS SOME BILLS ARE PENDING FOR SETTLEMENT BY CASHIER'
		END
	END
END
--END OF PROCEDURE - SP3S_CHECK_PENDING_BILLS
