

-- THIS FUNCTION RETURNS THE TAX_AC_CODE OR SALE_AC_CODE 
-- FROM LOCSST OR LOCSSTADD TABLE BASED ON THE SUPPLIED PARAMETERS
-- THIS FUNCTIONS IS USED TO DETERMINE THE RELATED TAX_AC_CODE AND SALE_AC_CODE
-- FROM TAX-STRUCTURE MODULE INSTEAD OF GETTING FROM CMD01106 TABLE
-- *R* 07062010 [MOHANLALSONS POSTING ISSUE]
CREATE FUNCTION FN_POSTACT_GETTAXACDETAILS
(
	@MEMODT				DATETIME,
	@PRODUCT_CODE		VARCHAR(100),
	@MRP				NUMERIC(14,2),
	@STATECODE			VARCHAR(20),
	@NOUTPUTMODE		INT
)
RETURNS VARCHAR(10)
--WITH ENCRYPTION
AS
BEGIN
	
	-- @NOUTPUTMODE = RETURN MODE: 1-SALE TAXACCODE; 2- SALEACCODE; 3- TAX PERCENTAGE; 4- PURCHASEACCODE; 5- PURCHASE TAXACCODE

	DECLARE @CMEMOID			VARCHAR(40),
			@CSUBSECTIONCODE	VARCHAR(20),
			@CSALETAXACCODE		VARCHAR(10),
			@CSALEACCODE		VARCHAR(10),
			@CPURCHASEACCODE	VARCHAR(10),
			@CPURCHASETAXACCODE	VARCHAR(10),
			@CTAXPERCENTAGE     VARCHAR(10)

	SELECT @CSUBSECTIONCODE = B.SUB_SECTION_CODE 
	FROM SKU A (NOLOCK)
	JOIN ARTICLE B (NOLOCK) ON A.ARTICLE_CODE = B.ARTICLE_CODE
	WHERE A.PRODUCT_CODE = @PRODUCT_CODE

	IF ISNULL(@CSUBSECTIONCODE,'') = ''
		GOTO ENDPROC
	
	SELECT TOP 1 @CMEMOID = A.MEMO_ID
	FROM LOCSST_MST A (NOLOCK)
	JOIN LOCSST B (NOLOCK) ON B.MEMO_ID=A.MEMO_ID
	WHERE A.FROM_DT <= @MEMODT 
	AND B.SUB_SECTION_CODE = @CSUBSECTIONCODE 
	AND A.STATE_CODE = @STATECODE
	ORDER BY A.FROM_DT DESC

	IF ISNULL(@CMEMOID,'') = ''
		GOTO ENDPROC

	-- GETTING TAX INFO FROM LOCSSTADD, IF VARIANCE FOUND.
	SELECT	@CSALETAXACCODE = A.TAX_AC_CODE, 
			@CSALEACCODE = A.SALE_AC_CODE,
			@CPURCHASEACCODE=A.PURCHASE_AC_CODE,
			@CPURCHASETAXACCODE=A.PURCHASE_TAX_AC_CODE,
			@CTAXPERCENTAGE = CONVERT(VARCHAR(10),A.TAX_PERCENTAGE)
	FROM LOCSSTADD A (NOLOCK)
	JOIN LOCSST B (NOLOCK) ON B.ROW_ID=A.LOCSST_ROW_ID
	WHERE B.MEMO_ID = @CMEMOID 
	AND B.SUB_SECTION_CODE = @CSUBSECTIONCODE 
	AND (ABS(@MRP) BETWEEN MIN_MRP AND MAX_MRP)

	IF @CSALETAXACCODE IS NULL
	BEGIN
		SELECT	@CSALETAXACCODE = B.TAX_AC_CODE, 
				@CSALEACCODE = B.SALE_AC_CODE,
				@CPURCHASEACCODE=B.PURCHASE_AC_CODE,
				@CPURCHASETAXACCODE=B.PURCHASE_TAX_AC_CODE,
			@CTAXPERCENTAGE = CONVERT(VARCHAR(10),B.TAX_PERCENTAGE)
		FROM LOCSST B (NOLOCK)
		WHERE B.MEMO_ID = @CMEMOID 
		AND B.SUB_SECTION_CODE = @CSUBSECTIONCODE 
	END
	
ENDPROC:
	IF @NOUTPUTMODE = 2 AND (ISNULL(@CSALEACCODE,'0000000000') = '0000000000')
		SELECT @CSALEACCODE = VALUE FROM CONFIG 
		WHERE CONFIG_OPTION ='SLS_RETAIL_SALE_AC_CODE'
		
 	RETURN (CASE WHEN @NOUTPUTMODE=1 THEN ISNULL(@CSALETAXACCODE,'0000000000')
			     WHEN @NOUTPUTMODE=3 THEN ISNULL(@CTAXPERCENTAGE,'0') 
			     WHEN @NOUTPUTMODE=4 THEN ISNULL(@CPURCHASEACCODE,'0000000000')
			     WHEN @NOUTPUTMODE=5 THEN ISNULL(@CPURCHASETAXACCODE,'0000000000') 
				 ELSE ISNULL(@CSALEACCODE,'0000000000') END)
END
--********************************* END OF FUNCTION FN_POSTACT_GETTAXACDETAILS
